use crate::models::{Vec3};

#[starknet::interface]
pub trait IGameActions<T> {
    fn ship_spawn(ref self: T, spaceship_id: u128, spawn_pos: Vec3);
    fn ship_despawn(ref self: T, spaceship_id: u128);
    fn ship_board(ref self: T, spaceship_id: u128);
    fn ship_unboard(ref self: T, spaceship_id: u128, pos: Vec3);
    fn ship_move(ref self: T, spaceship_id: u128, destination: Vec3, p_hyperspeed: bool);
    fn ship_switch_reference_body(ref self: T, spaceship_id: u128, reference_body: u128, position: Vec3, direction: Vec3);
    fn player_move(ref self: T, dst: Vec3);
    fn item_collect(ref self: T, player_id: u128, collectable_type: u16, collectable_index: u8);
}


#[dojo::contract]
pub mod GameActions {
    use super::IGameActions;
    use crate::models::{Player, Spaceship, Planet, CollectableTracker, PlayerPosition, ShipPosition, Vec3, InventoryItem};
    
    use dojo::world::world;
    use starknet::get_block_timestamp;
    use core::num::traits::Pow;
    use core::num::traits::Sqrt;
    use array::ArrayTrait;
    use core::byte_array::ByteArray;
    use starknet::{ContractAddress, get_caller_address};
    use dojo::model::{ModelStorage};
    use dojo::event::EventStorage;
    use core::traits::BitAnd;

    const FP_UNIT: i128 = 0x10000000000; // 2^40
    const FP_UNIT_BITS: u8 = 40;

    fn current_pos(pos: Vec3, dest: Vec3, dir: Vec3, last_move: u128, speed: u128) -> Vec3 {
        let current_time_u64 = get_block_timestamp();
        let current_time: u128 = current_time_u64.into();
        let time_delta : u128 = current_time - last_move;
        
        let distance_elapsed : u128 = time_delta * speed;
        let distance_elapsed_sq: u256 = fp40_sq(distance_elapsed.try_into().unwrap());

        let distance_to_dest_sqf: felt252 = vec3_fp40_dist_sq(pos, dest).try_into().unwrap();
        let distance_to_dest_sq: u256 = distance_to_dest_sqf.into();

        if (distance_to_dest_sq <= distance_elapsed_sq) {
            return dest;
        };

        let distancei :i128 = distance_elapsed.try_into().unwrap();
        return Vec3 {
            x: pos.x + fp40_mul(dir.x, distancei),
            y: pos.y + fp40_mul(dir.y, distancei),
            z: pos.z + fp40_mul(dir.z, distancei),
        };
    }

    fn fp40_mul(a: i128, b: i128) -> i128 {
        let mut ret = a * b;
        ret = ret / 2_i128.pow(FP_UNIT_BITS.into());
        return ret;
    }

    fn fp40_sq(a: i128) -> u256 {
        let abs : u256 = abs_value(a).into();
        let ret = (abs * abs) / 2_u256.pow(FP_UNIT_BITS.into());
        return ret;
    }

    fn fp40_sqrt(a: u128) -> u128 {
        let abs : u256 = a.into() * 2_u256.pow(FP_UNIT_BITS.into());
        let sqrt = abs.sqrt();
        return sqrt.try_into().unwrap();
    }

    fn abs_value(v: i128) -> u128 {
        if (v < 0) { return (v * -1).try_into().unwrap(); };
        return v.try_into().unwrap();
    }

    fn fp40_div(a: i128, b: i128) -> i128 {
        let a_abs256: u256 = (abs_value(a) * 2_u128.pow(FP_UNIT_BITS.into())).into();
        let b_abs: u256 = (abs_value(b)).into();
        let abs_ret = a_abs256 / b_abs;
        let uret : u128 = abs_ret.try_into().unwrap(); 
        let ret : i128 = uret.try_into().unwrap();
        if ( (a < 0) != (b < 0) ) {
            return ret * -1;
        };
        return ret;
    }

    fn vec3_fp40_div_scalar(v1: Vec3, s: i128) -> Vec3 {
        let ret = Vec3 {
            x: fp40_div(v1.x, s),
            y: fp40_div(v1.y, s),
            z: fp40_div(v1.z, s),
        };
        return ret;
    }

    fn vec3_fp40_dist_sq(v1: Vec3, v2: Vec3) -> i128 {
        let dx = v1.x - v2.x;
        let dy = v1.y - v2.y;
        let dz = v1.z - v2.z;
        let distance_squared: u128 = (fp40_sq(dx) + fp40_sq(dy) + fp40_sq(dz)).try_into().unwrap();
        return distance_squared.try_into().unwrap();
    }

    fn vec3_fp40_len_sq(vec: Vec3) -> i128 {
        let d2 = fp40_sq(vec.x) + fp40_sq(vec.y) + fp40_sq(vec.z);
        let distance_squared: u128 = d2.try_into().unwrap();
        return distance_squared.try_into().unwrap();
    }

    fn vec3_fp40_len(vec: Vec3) -> i128 {
        let d2 : u128 = vec3_fp40_len_sq(vec).try_into().unwrap();
        let d = fp40_sqrt(d2);
        return d.try_into().unwrap();
    }

    fn vec3_sub(v1: Vec3, v2: Vec3) -> Vec3 {
        return Vec3 {
            x: v1.x - v2.x,
            y: v1.y - v2.y,
            z: v1.z - v2.z,
        };
    }

    const DEFAULT_REFERENCE_BODY_ID: u128 = 0;
    const MAX_SPAWN: u8 = 128;
    const FP_LEN_SQ_EPSION: i128 = 0x40000000; // 2^30

    const AREA_SIZE: i128 = 32;
    const PLAYER_WALKING_SPEED: i128 = 1 * FP_UNIT;
    const MAX_PLAYER_WALK_EPSILON2: i128 = 5 * FP_UNIT;
    const MAX_SPAWN_DISTANCE_SQUARED: i128 = 25 * FP_UNIT; // 5 meters
    const MAX_ITEM_PICKUP_D2: i128 = 64 * FP_UNIT; // 8 meters

    const SHIP_SPEED: i128 = 100 * FP_UNIT;
    const SHIP_HYPER_SPEED: i128 = 1000 * FP_UNIT;

    pub mod ShipFlags {
        pub const Spawned: u8 = 1;
        pub const Landed: u8 = 2;
        pub const Occupied: u8 = 4;
    }

    pub mod PlayerFlags {
        pub const OnFoot: u8 = 1;
        pub const OnShip: u8 = 2;
    }

    #[abi(embed_v0)]
    impl GameActionsImpl of IGameActions<ContractState> {

        fn ship_spawn(ref self: ContractState, spaceship_id: u128, spawn_pos: Vec3) {
            let mut world = self.world_default();
            let player_id = get_caller_address();
            let player : Player = world.read_model(player_id);
            let mut ship : Spaceship = world.read_model(spaceship_id);

            assert(ship.owner == player_id, 'NotOwner');
            assert((ship.status_flags & ShipFlags::Occupied) == 0, 'ShipNotEmpty');

            let player_pos_model : PlayerPosition = world.read_model(player_id);
            let player_pos = current_pos(player_pos_model.pos, player_pos_model.dest, player_pos_model.dir, player_pos_model.last_motion, PLAYER_WALKING_SPEED.try_into().unwrap());
            
            let distance_squared = vec3_fp40_dist_sq(spawn_pos, player_pos);
            assert(distance_squared <= MAX_SPAWN_DISTANCE_SQUARED, 'TooFar');

            if ((ship.status_flags & ShipFlags::Landed) == 0) {
                ship.status_flags += ShipFlags::Landed;
            };
            if ((ship.status_flags & ShipFlags::Spawned) == 0) {
                ship.status_flags += ShipFlags::Spawned;
            };

            ship.reference_body = player.reference_body;
            world.write_model(@ship);

            let mut ship_motion : ShipPosition = world.read_model(spaceship_id);
            ship_motion.pos = spawn_pos;
            ship_motion.dest = spawn_pos;
            ship_motion.dir = player_pos_model.dir;
            ship_motion.last_motion = get_block_timestamp().into();
            world.write_model(@ship_motion);
        }

        fn ship_despawn(ref self: ContractState, spaceship_id: u128) {
            let mut world = self.world_default();
            let player_id = get_caller_address();
            let mut ship : Spaceship = world.read_model(spaceship_id);
            assert(ship.owner == player_id, 'NotOwner');
            assert((ship.status_flags & ShipFlags::Spawned) != 0, 'AlreadyDeSpawned');
            assert((ship.status_flags & ShipFlags::Occupied) == 0, 'ShipNotEmpty');

            ship.status_flags -= ShipFlags::Spawned;
            world.write_model(@ship);
        }

        fn ship_board(ref self: ContractState, spaceship_id: u128) {
            let mut world = self.world_default();
            let player_id = get_caller_address();
            let mut ship : Spaceship = world.read_model((spaceship_id, player_id));
            
            assert((ship.status_flags & ShipFlags::Spawned) != 0, 'ShipNotSpawned');
            assert((ship.status_flags & ShipFlags::Occupied) == 0, 'ShipAlreadyOccupied');

            let mut player : Player = world.read_model(player_id);
            assert((player.status_flags & PlayerFlags::OnFoot) != 0, 'PlayerNotWalking');
            assert((player.status_flags & PlayerFlags::OnShip) == 0, 'PlayerAlreadyOnSpaceship');

            let ship_pos : ShipPosition = world.read_model(spaceship_id);
            let mut player_pos_model : PlayerPosition = world.read_model(player_id);
            let player_pos = current_pos(player_pos_model.pos, player_pos_model.dest, player_pos_model.dir, player_pos_model.last_motion, PLAYER_WALKING_SPEED.try_into().unwrap()); 

            let dist2 = vec3_fp40_dist_sq(player_pos, ship_pos.pos);
            assert(dist2 <= MAX_SPAWN_DISTANCE_SQUARED, 'TooFar');

            ship.status_flags += ShipFlags::Occupied;
            world.write_model(@ship);

            player.status_flags -= PlayerFlags::OnFoot;
            player.status_flags += PlayerFlags::OnShip;
            world.write_model(@player);
        }

        fn ship_unboard(ref self: ContractState, spaceship_id: u128, pos: Vec3) {
            let mut world = self.world_default();
            let player_id = get_caller_address();
            let mut ship : Spaceship = world.read_model((spaceship_id, player_id));
            assert((ship.status_flags & ShipFlags::Occupied) != 0, 'ShipNotOccupied');
            
            let ship_pos : ShipPosition = world.read_model(spaceship_id);
            let dist2 = vec3_fp40_dist_sq(ship_pos.pos, pos);
            assert(dist2 <= MAX_SPAWN_DISTANCE_SQUARED, 'TooFar');

            let mut player : Player = world.read_model(player_id);
            assert((player.status_flags & PlayerFlags::OnFoot) == 0, 'PlayerWalking');
            assert((player.status_flags & PlayerFlags::OnShip) != 0, 'PlayerNotOnSpaceship');

            let mut player_pos : PlayerPosition = world.read_model(player_id);
            player_pos.pos = pos;
            player_pos.dest = pos;
            world.write_model(@player_pos);

            ship.status_flags -= ShipFlags::Occupied;
            world.write_model(@ship);

            player.status_flags -= PlayerFlags::OnShip;
            player.status_flags += PlayerFlags::OnFoot;
            world.write_model(@player);
        }

        fn ship_move(ref self: ContractState, spaceship_id: u128, destination: Vec3, p_hyperspeed: bool) {
            let mut world = self.world_default();
            let player_id = get_caller_address();
            let ship : Spaceship = world.read_model((spaceship_id, player_id));

            assert((ship.status_flags & ShipFlags::Spawned) != 0, 'Ship not spawned');
            assert((ship.status_flags & ShipFlags::Occupied) != 0, 'Ship not being driven by player');

            let mut ship_pos_model : ShipPosition = world.read_model(spaceship_id);
            let mut speed_mode : u64 = SHIP_SPEED.try_into().unwrap();
            if (p_hyperspeed) {
                assert(ship.reference_body == DEFAULT_REFERENCE_BODY_ID, 'Hyperspeed not possible');
                speed_mode = SHIP_HYPER_SPEED.try_into().unwrap();
            };
            let ship_pos = current_pos(ship_pos_model.pos, ship_pos_model.dest, ship_pos_model.dir, ship_pos_model.last_motion, speed_mode.into());

            let dif : Vec3 = vec3_sub(destination, ship_pos);
            let len = vec3_fp40_len(dif);
            let dir = vec3_fp40_div_scalar(dif, len);
            
            let new_ship_pos = ShipPosition {
                ship: spaceship_id,
                pos: ship_pos,
                dir: dir,
                dest: destination,
                last_motion: get_block_timestamp().into(),
                hyperspeed: p_hyperspeed,
            };
            world.write_model(@new_ship_pos);
        }

        fn ship_switch_reference_body(ref self: ContractState, spaceship_id: u128, reference_body: u128, position: Vec3, direction: Vec3) {
            let mut world = self.world_default();
            let player_id = get_caller_address();
            let mut ship : Spaceship = world.read_model((spaceship_id, player_id));

            assert((ship.status_flags & ShipFlags::Spawned) != 0, 'Ship not spawned');
            assert((ship.status_flags & ShipFlags::Occupied) != 0, 'Ship not being driven by player');

            let len2 = vec3_fp40_len_sq(direction);
            assert(len2 >= FP_UNIT - FP_LEN_SQ_EPSION && len2 <= FP_UNIT + FP_LEN_SQ_EPSION, 'Direction not normalized');

            ship.reference_body = reference_body;
            world.write_model(@ship);

            let mut ship_pos : ShipPosition = world.read_model(spaceship_id);
            ship_pos.pos = position;
            ship_pos.dest = position;
            ship_pos.hyperspeed = false;
            ship_pos.dir = direction;

            world.write_model(@ship_pos);
        }

        fn player_move(ref self: ContractState, dst: Vec3) {
            let mut world = self.world_default();
            let player_id = get_caller_address();
            let mut player : Player = world.read_model(player_id);
            if (player.status_flags == 0) { 
                player.status_flags = PlayerFlags::OnFoot;
                world.write_model(@player);
            }
            assert((player.status_flags & PlayerFlags::OnFoot) != 0, 'Player is not walking');

            let mut player_pos_model : PlayerPosition = world.read_model(player_id);
            if (player_pos_model.last_motion == 0) { 
                player_pos_model.last_motion = get_block_timestamp().into();
            }
            let model_pos = current_pos(player_pos_model.pos, player_pos_model.dest, player_pos_model.dir, player_pos_model.last_motion, PLAYER_WALKING_SPEED.try_into().unwrap());

            let dif : Vec3 = vec3_sub(dst, model_pos);
            let len = vec3_fp40_len(dif);
            let mut dir = dif;
            if (len > 0) {
                dir = vec3_fp40_div_scalar(dif, len);
            };

            let new_player_pos = PlayerPosition {
                player: player_id,
                pos: model_pos,
                dir: dir,
                dest: dst,
                last_motion: get_block_timestamp().into(),
            };
            world.write_model(@new_player_pos);
        }

        fn item_collect(ref self: ContractState, player_id: u128, collectable_type: u16, collectable_index: u8) {
            let mut world = self.world_default();
            let player_id = get_caller_address();
            let player : Player = world.read_model(player_id);
            let planet : Planet = world.read_model(player.reference_body);
            let player_pos_model : PlayerPosition = world.read_model(player_id);
            let player_pos = current_pos(player_pos_model.pos, player_pos_model.dest, player_pos_model.dir, player_pos_model.last_motion, PLAYER_WALKING_SPEED.try_into().unwrap());

            let area_x = player_pos.x / (AREA_SIZE * FP_UNIT).into();
            let area_y = player_pos.y / (AREA_SIZE * FP_UNIT).into();
            let area_z = player_pos.z / (AREA_SIZE * FP_UNIT).into();
            let area_hash = area_x * 31 * 31 + area_y * 31 + area_z;

            let mut count_seed = ByteArray { data: array![], pending_word: 0, pending_word_len: 0 };
            count_seed.append_byte(planet.seed.try_into().unwrap());
            count_seed.append_byte(planet.epoc.try_into().unwrap());
            count_seed.append_byte(area_hash.try_into().unwrap());
            count_seed.append_byte(collectable_type.try_into().unwrap());
            
            let count_hash = core::sha256::compute_sha256_byte_array(@count_seed);
            let total_spawned = *count_hash.span().at(7) % MAX_SPAWN.into();

            assert(collectable_index.into() < total_spawned, 'InvalidIndex');

            let mut pos_seed = ByteArray { data: count_seed.data.clone(), pending_word: 0, pending_word_len: 0 };
            pos_seed.append_byte(collectable_index.into());
            let item_hash = core::sha256::compute_sha256_byte_array(@pos_seed);

            let span = item_hash.span();
            let span_0_i128: i128 = (*span.at(0)).try_into().unwrap();
            let span_1_i128: i128 = (*span.at(1)).try_into().unwrap();
            let span_2_i128: i128 = (*span.at(2)).try_into().unwrap();
            
            let offset_x = (span_0_i128 * FP_UNIT / 0xFFFFFFFF_i128) * AREA_SIZE;
            let offset_y = (span_1_i128 * FP_UNIT / 0xFFFFFFFF_i128) * AREA_SIZE;
            let offset_z = (span_2_i128 * FP_UNIT / 0xFFFFFFFF_i128) * AREA_SIZE;

            let item_pos = Vec3 {
                x: area_x * FP_UNIT + offset_x,
                y: area_y * FP_UNIT + offset_y,
                z: area_z * FP_UNIT + offset_z,
            };

            let d2 = vec3_fp40_dist_sq(item_pos, player_pos);
            assert(d2 <= MAX_ITEM_PICKUP_D2, 'TooFar');

            let area_key: i128 = area_hash * 1000_i128 + collectable_type.into();
            
            let mut tracker : CollectableTracker = world.read_model(area_key);
            
            let bitfield : u128 = if tracker.epoc == planet.epoc { tracker.bitfield } else { 0 };
            let bit_mask : u128 = 2_u128.pow(collectable_index.into());

            let is_already_collected = (bitfield & bit_mask) != 0;
            assert(!is_already_collected, 'AlreadyCollected');

            tracker.bitfield = bitfield | bit_mask;
            tracker.epoc = planet.epoc;
            world.write_model(@tracker);

            let current_item : InventoryItem = world.read_model((player_id, collectable_type));
            
            let new_item = InventoryItem { 
                player_id: player_id, 
                item_type: collectable_type, 
                count: current_item.count + 1, 
            };
            
            world.write_model(@new_item);
        }
    }

    #[generate_trait]
    impl InternalImpl of InternalTrait {
        fn world_default(self: @ContractState) -> dojo::world::WorldStorage {
            self.world(@"utp_dojo")
        }
    }
}
